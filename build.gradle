buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0.0"
group = "guichaguri.doha-blocker"
archivesBaseName = "DOHA-Blocker"

sourceCompatibility = targetCompatibility = "1.6"
compileJava {
    sourceCompatibility = targetCompatibility = "1.6"
}

minecraft {
    version = "1.11.2-13.20.0.2282"
    runDir = "run"
    mappings = "snapshot_20161220"
}

reobf {
    jar {
        extraLines 'PK: com/eclipsesource/json guichaguri/dohablocker/json'
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'bungeecord'
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    // Sponge
    compile 'org.spongepowered:spongeapi:5.1.0'

    // Bukkit
    compile 'org.bukkit:bukkit:1.11.2-R0.1-SNAPSHOT'

    // BungeeCord
    compile 'net.md-5:bungeecord-api:1.10-SNAPSHOT'

    // Json
    shade 'com.eclipsesource.minimal-json:minimal-json:0.9.4'
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
