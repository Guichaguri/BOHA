buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:1.12'
    }
}

import org.yaml.snakeyaml.Yaml
import org.yaml.snakeyaml.DumperOptions
import org.yaml.snakeyaml.DumperOptions.FlowStyle;

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

group 'guichaguri.doha-blocker'
version ver

sourceCompatibility = targetCompatibility = "1.6"
compileJava {
    sourceCompatibility = targetCompatibility = "1.6"
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'bungeecord'
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    // Common
    compile project(':common')

    // Sponge
    compile 'org.spongepowered:spongeapi:5.1.0'

    // Bukkit
    compile 'org.bukkit:bukkit:1.11.2-R0.1-SNAPSHOT'

    // BungeeCord
    compile 'net.md-5:bungeecord-api:1.10-SNAPSHOT'

    // Json
    compile 'com.eclipsesource.minimal-json:minimal-json:0.9.4'
}

def processYaml(yaml, file) {
    println("Updating version from " + file.name)

    def data = (Map)yaml.load(file.text)
    data.put("version", ver);
    file.text = yaml.dump(data)
}

processResources {
    def options = new DumperOptions();
    options.prettyFlow = true;
    options.defaultFlowStyle = FlowStyle.BLOCK
    def yaml = new Yaml(options)

    processYaml(yaml, file("src/main/resources/plugin.yml"))
    processYaml(yaml, file("src/main/resources/bungee.yml"))
}

shadowJar {
    baseName = 'DOHA-Blocker-Plugin'
    classifier = null

    dependencies {
        include project(':common')
        include dependency('com.eclipsesource.minimal-json:minimal-json:0.9.4')
    }

    relocate 'com.eclipsesource.json', 'guichaguri.dohablocker.json'
}